# Maintainer: Dani Rodr√≠guez <dani@danirod.es>
pkgname=i386-elf-binutils
pkgver=2.25.1
pkgrel=1
pkgdesc="GNU Binutils targetting i386-elf"
arch=('i686' 'x86_64')
url="https://www.gnu.org/software/binutils/"
license=('GPL')
groups=()
depends=(zlib)
source=("http://ftpmirror.gnu.org/binutils/binutils-$pkgver.tar.bz2")
sha256sums=("b5b14added7d78a8d1ca70b5cb75fef57ce2197264f4f5835326b0df22ac9f22")

build() {
        mkdir -p "binutils-build"
        cd "binutils-build"
	../binutils-$pkgver/configure --prefix=/usr \
                                      --target=i386-elf \
                                      --disable-multilib \
                                      --disable-nls \
                                      --disable-werror

        # Default CPPFLAGS is known to cause issues
        # See https://bbs.archlinux.org/viewtopic.php?id=212317
        make CPPFLAGS=''
}

check() {
        cd binutils-build
        make -k check
}

package() {
        cd binutils-build
        make install DESTDIR=$pkgdir

        # These files conflict with host binutils
        rm -rf $pkgdir/usr/share/info/
}
