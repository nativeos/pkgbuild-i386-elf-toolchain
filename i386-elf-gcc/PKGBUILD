# Maintainer: Dani Rodr√≠guez <dani@danirod.es>
pkgname=i386-elf-gcc
pkgver=6.3.0
pkgrel=1
pkgdesc="GNU Compiler Collection targetting i386-elf"
arch=('i686' 'x86_64')
url="https://gcc.gnu.org"
license=('GPL')
groups=()
depends=(zlib libmpc)
makedepends=(gmp mpfr)
source=("http://ftpmirror.gnu.org/gcc/gcc-$pkgver/gcc-$pkgver.tar.bz2")
sha256sums=("f06ae7f3f790fbf0f018f6d40e844451e6bc3b7bc96e128e63b09825c1f8b29f")

build() {
        mkdir -p "gcc-build"
        cd "gcc-build"
        ../gcc-$pkgver/configure --prefix=/usr \
                                 --target=i386-elf \
                                 --disable-multilib \
                                 --disable-nls \
                                 --disable-werror \
                                 --without-headers \
                                 --enable-languages=c,c++

        make all-gcc CPPFLAGS=''
        make all-target-libgcc CPPFLAGS=''
}

package() {
        cd gcc-build
        make install-gcc DESTDIR=$pkgdir
        make install-target-libgcc DESTDIR=$pkgdir

        # Some files will cause conflict with host tools.
        rm -rf $pkgdir/usr/share/info
        rm -rf $pkgdir/usr/share/man/man7
}
