# Maintainer: Dani Rodr√≠guez <dani@danirod.es>
pkgname=i386-elf-gdb
pkgver=7.12
pkgrel=1
pkgdesc="GNU Project Debugger targetting i386-elf"
arch=('i686' 'x86_64')
url="https://www.gnu.org/software/gdb/"
license=('GPL')
groups=()
depends=(zlib)
source=("http://ftpmirror.gnu.org/gdb/gdb-$pkgver.tar.xz")
sha256sums=("834ff3c5948b30718343ea57b11cbc3235d7995c6a4f3a5cecec8c8114164f94")

build() {
        cd "gdb-$pkgver"
	./configure --prefix=/usr \
                    --target=i386-elf \
                    --program-prefix=i386-elf- \
                    --disable-werror
        make CPPFLAGS=''
}

package() {
        cd "gdb-$pkgver"
        make install DESTDIR=$pkgdir

        # These files conflict with host gdb and with i386-elf-binutils
        # They should be provided by host gdb anyway. I'm not 100% sure.
        rm -rf $pkgdir/usr/include/
        rm -rf $pkgdir/usr/share/info/
        rm -rf $pkgdir/usr/share/gdb/
        rm -rf $pkgdir/usr/share/locale/
}
